#include "std\lodNames.inc"
console=openStandardIO;

//example usage
//O2Script.exe -a O2Scripts\NamedSelectionsOverview.bio2s p:\test\name.p3d >> p:\test\NamedSelectionsOverview.txt

//Library methods
//==================

_createOverviewForCurrentLOD =
{
	private["_object"];
	_object = _this select 0;

	{
		private["_selectionName","_textures","_selection","_selectedFaces"];
		_selectionName = _x;

		_textures = [];
		_selection = _object loadSelection _selectionName;
		_selectedFaces = getSelectedFaces _selection;

		{
			private["_face","_texture"];
			_face = _x;

			_texture = getTexture (_object face _face);
			if (!(_texture in _textures)) then
			{

				_textures set [count _textures,_texture];
			};
		} forEach _selectedFaces;

		console<<"_selectionName: "<<_selectionName<<eoln;

		{
			private["_texture"];
			_texture = _x;
			if (_texture != "") then
			{
				console<<"_textures: "<<_texture<<eoln;
			};
		} forEach _textures;
	} forEach (getSelections _object);
};

// main
_sourcePath = this@0;

_p3d = newLODObject;
_p3d loadP3D _sourcePath;

_LODs = getObjects _p3d;
_resolutions = getResolutions _p3d;

for "_i" from 0 to ((count _p3d) - 1) do
{
	private["_currentLOD","_currentResolution"];
	_currentLOD = _LODs @ _i;
	_currentResolution = _resolutions @ _i;

	if (IS_LOD_RESOLUTION(_currentResolution)) then
	{
		console<<"_currentResolution: "<<str _currentResolution<<eoln;

		[_currentLOD] call _createOverviewForCurrentLOD;
	};
};
